# 言語仕様

```bnf
<プログラム> ::= <関数定義集合> 
             | <変数宣言部> <関数定義集合>

<関数定義集合> ::= <関数定義> 
             | <関数定義> <関数定義集合>

<関数定義> ::= func <識別子> ( <仮引数リスト> ) { <変数宣言部> <文集合> }
             | func <識別子> ( <仮引数リスト> ) { <文集合> }

<仮引数リスト> ::= <識別子集合> | ε

<変数宣言部> ::= <宣言文> <変数宣言部> | <宣言文>

<宣言文> ::= define <識別子集合>; 
           | array <識別子> <次元リスト>;
           | reg <識別子集合>;

<次元リスト> ::= [ <式> ] | [ <式> ] <次元リスト>

<識別子集合> ::= <識別子> , <識別子集合> | <識別子>

<文集合> ::= <文> <文集合> | <文>

<文> ::= <代入文> 
       | <ループ文> 
       | <条件分岐文> 
       | <関数呼び出し文> 
       | <return文>
       | <break文>

<代入文> ::= <左辺値> = <式>;

<ループ文> ::= while ( <条件式> ) { <文集合> }

<条件分岐文> ::= if ( <条件式> ) { <文集合> } 
             | if ( <条件式> ) { <文集合> } else { <文集合> }

<関数呼び出し文> ::= funccall <識別子> ( <実引数リスト> );

<return文> ::= return <式>;
             | return;

<break文> ::= break;

<実引数リスト> ::= <式> , <実引数リスト> 
                | <式> 
                | ε

<条件式> ::= <式> <比較演算子> <式>

<式> ::= <式> <演算子> <式>
       | <単項演算子> <式>
       | <式> <後置演算子>
       | <変数>
       | <関数呼び出し式>
       | ( <式> )

<関数呼び出し式> ::= funccall <識別子> ( <実引数リスト> )

<比較演算子> ::= == | != | < | > | <= | >=

<演算子> ::= + | - | * | / | % 
           | << | >> 
           | & | ^ | |

<単項演算子> ::= @  /* アドレス取得 */

<後置演算子> ::= $  /* ポインタ参照 (dereference) */

<変数> ::= <数> | <左辺値>

<左辺値> ::= <識別子> 
           | <左辺値> [ <式> ]
           | <式> $

<識別子> ::= <英字> <英数字列> | <英字>
<英数字列> ::= <英数字> <英数字列> | <英数字>
<英数字> ::= <英字> | <数字>
<数> ::= <数字> <数> | <数字>
<英字> ::= a-z | A-Z
<数字> ::= 0-9
```

## 補足

- ε は「空（何もなし）」を表します。

- ポインタ:
  - @a で a のアドレスを取得します（前置演算子）。
  - p$ でポインタ p の指す値を参照します（後置演算子）。

- ビット演算: & (AND), | (OR), ^ (XOR), <<, >> (Shift) が使用可能です。

- 配列宣言: `array a[10][20];` のような多次元配列宣言が可能です。

- レジスタ変数: reg a; のように宣言することで、レジスタ割り当てを意図した変数定義が可能です。
  - ただし、引数と合わせて8つまで使用可能です。

- コメント:
  - // から行末まで
  - /\*から\*/ まで 上記の形式がコメントとして無視されます。
